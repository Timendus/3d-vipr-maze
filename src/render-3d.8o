#####################
# Render the map to the screen in pseudo-3D
# (for the actual gameplay)
: render-3d
  column-two
  column-three
  column-one
  jump column-four

: column-one
  i := long nodes-column-one
  find-sprite
  decompress
  v0 := 0
  jump render-column

: column-two
  i := long nodes-column-two
  find-sprite
  decompress
  v0 := 16
  jump render-column

: column-three
  i := long nodes-column-three
  find-sprite
  decompress
  v0 := 32
  jump render-column

: column-four
  i := long nodes-column-four
  find-sprite
  decompress
  v0 := 48
  # Fall through

: render-column
  i := decompression-buffer
  v2 := 15

  v1 := 1
  sprite v0 v1 15
  i += v2
  v1 := 16
  sprite v0 v1 15
  i += v2
  v0 += 8

  v1 := 1
  sprite v0 v1 15
  i += v2
  v1 := 16
  sprite v0 v1 15
  return

#####################
# This routine walks through the binary tree with nodes and leafs
# Input: i pointing to a node
# Output: i pointing to the image to be rendered
: find-sprite
  # Load this node and find what's there
  load v2
  v5 := v1
  v6 := v2
  # Coordinates are stored together in first byte, so unpack
  v1 := 0x0F
  v4 := v0
  v4 &= v1
  v0 >>= v0
  v0 >>= v0
  v0 >>= v0
  v0 >>= v0
  v0 &= v1
  v3 := v0
  look-ahead
  # Did we encounter a wall?
  if v0 == 1 begin
    v0 := v6
  else
    v0 := v5
  end
  # Is the next step a node or a leaf?
  v1 := 0x80
  v2 := v0
  v2 &= v1
  if v2 == 0x80 begin
    # Next is a leaf, extract the pointer
    i := long leafs
    v1 := 0b01111111
    v0 &= v1 # AND out the leaf indicator bit
    v0 <<= v0
    i += v0
    load v1
    # We now have the address of the leaf image in v0 and v1
    return
  else
    # Next is a node, load it and restart
    i := long nodes
    i += v0
    i += v0
    i += v0
    jump find-sprite
  end
