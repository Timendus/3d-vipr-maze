#####################
# 3D Viper Maze
# An Octojam 7 entry
# By Timendus
# https://github.com/Timendus/3d-viper-maze

: main
  # Prepare virtual timers
  viper-delay := 1
  v0 := 1
  delay := v0

  # Start with training map
  send-player-back-to-start

: main-redraw
  clear
  render-3d
  mark-screen-clean
: main-loop
  clock-tick
  check-keys
  i := long dirty    # Is the screen dirty?
  load v0
  if v0 == 1 then jump main-redraw
  jump main-loop

# Handle those things that need attention in parallel to gameplay,
# like sound and enemy movement. A virtual timer implementation in the upper
# registers.
: clock-tick
  v0 := delay
  if v0 != 0 then return
  viper-delay -= 1
  if viper-delay == 0 then vipers
  v0 := 1
  delay := v0
  return

# Mark the screen dirty or clean to manage redraws

: mark-screen-dirty
  i := long dirty
  v0 := 1
  save v0
  return

: mark-screen-clean
  i := long dirty
  v0 := 0
  save v0
  return
